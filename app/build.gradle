import com.android.build.gradle.api.ApplicationVariant

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "org.mozilla.screenshot.go"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "0.8"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def build_number = System.getenv("BITRISE_BUILD_NUMBER")
        if (build_number?.trim()) {
            def build_number_with_offset = build_number.toInteger()
            versionCode build_number_with_offset
            versionNameSuffix "(" + build_number_with_offset + ")"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix applicationIdSuffix
        }
    }

    flavorDimensions "product"

    productFlavors {
        go {
            resConfigs "en", "in"
            dimension "product"
        }

        preview {
            applicationId "gro.allizom.scryer"
            applicationIdSuffix ""
            versionNameSuffix ".nightly"
            resConfigs "en", "in"
            dimension "product"
        }
    }

    variantFilter { variant ->
        def flavors = variant.flavors*.name
        // We only need a gecko debug and beta build for now.
        if (flavors.contains("preview") && variant.buildType.name != "release") {
            setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        buildConfigField("String", "ADJUST_TOKEN", "\"${getAdjustToken()}\"")
        buildConfigField("String", "ADJUST_ENVIRONMENT", getAdjustEnvironment(variant))
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${Versions.support}"
    implementation "com.android.support:design:${Versions.support}"
    implementation "com.android.support:cardview-v7:${Versions.support}"
    implementation "com.android.support:recyclerview-v7:${Versions.support}"
    implementation "com.android.support:transition:${Versions.support}"
    implementation "com.android.support:preference-v14:${Versions.support}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${Versions.kotlin_coroutine}"

    implementation "com.android.support.constraint:constraint-layout:${Versions.constraint_layout}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${Versions.kotlin}"

    testImplementation "junit:junit:${Versions.junit}"
    androidTestImplementation "com.android.support.test:runner:${Versions.test_runner}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${Versions.espresso}"

    implementation "android.arch.lifecycle:extensions:${Versions.arch_lifecycle}"
    implementation "android.arch.lifecycle:common-java8:${Versions.arch_lifecycle}"

    implementation "android.arch.navigation:navigation-fragment:${Versions.arch_navigation}"
    implementation "android.arch.navigation:navigation-ui:${Versions.arch_navigation}"

    implementation "android.arch.persistence.room:runtime:${Versions.arch_room}"
    kapt "android.arch.persistence.room:compiler:${Versions.arch_room}"

    implementation "com.github.bumptech.glide:glide:${Versions.glide}"

    implementation "com.google.firebase:firebase-core:${Versions.firebase}"
    implementation "com.google.firebase:firebase-messaging:${Versions.firebase_messageing}"
    implementation "com.google.firebase:firebase-ml-vision:${Versions.firebase_ml}"

    implementation "com.crashlytics.sdk.android:crashlytics:${Versions.crashlytics}"

    implementation "org.mozilla.components:service-telemetry:${Versions.android_components}"

    testImplementation "org.mockito:mockito-core:${Versions.mockito}"

    implementation "com.adjust.sdk:adjust-android:${Versions.adjust}"
    implementation "com.android.installreferrer:installreferrer:${Versions.android_install_referrer}"
    implementation "com.google.android.gms:play-services-analytics:${Versions.firebase}" // Required by Adjust
}
repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

static def getAdjustToken() {
    return System.getenv("ADJUST_TOKEN") ?: ""
}

static def getAdjustEnvironment(ApplicationVariant variant) {
    return (variant.buildType.name == "release") ?
            "com.adjust.sdk.AdjustConfig.ENVIRONMENT_PRODUCTION" :
            "com.adjust.sdk.AdjustConfig.ENVIRONMENT_SANDBOX"
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android-extensions'
